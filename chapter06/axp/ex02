#!/usr/bin/env perl

use 5.014;
use warnings;
use List::Util qw ( max );
use File::Slurp qw ( read_file write_file );
use JSON;

my $run = max map { s/\D//gr } glob( "tally*.json" );
my %total_bytes;

if ( defined $run ) {
	%total_bytes = %{ from_json read_file "tally$run.json" } or die "Couldn't read from tally file\n";
}

while (<>) {
  next if /^#/;
  my ($source, $destination, $bytes) = split;
  $total_bytes{$source}{$destination} += $bytes;
}

foreach my $source ( sort keys %total_bytes ) {
	say $source;
	
	foreach my $dest ( sort keys %{ $total_bytes{$source} } ) {
		say "  $dest $total_bytes{$source}{$dest}";
	}
}

$run++;
write_file "tally$run.json", to_json \%total_bytes or die "Couldn't write to tally file\n";
say "\n----------\nStored to tally$run.json";
