#!/usr/bin/env perl

use 5.014;
use warnings;
use File::Slurp qw( read_dir );
use Data::Dumper;

my $dir = $ARGV[0] // '.';
dump_data_for_path( data_for_path( $dir ) );

sub data_for_path {
	my $path = shift;
	
	return undef if ( -f $path or -l $path ); # files or symbolic links
	return { map { $_ => data_for_path( "$path/$_" ) } grep /^[^.]/, ( read_dir $path ) } if ( -d $path );

	warn "$path is neither a file nor a directory\n";
	return undef;
}

sub dump_data_for_path {
	my $data = shift;
	my $indent = shift // "";
	
	return unless ( defined $data );
	
	foreach my $k ( sort keys %$data ) {
		if ( defined $data->{ $k } ) {
			if ( %{ $data->{ $k } } ) {
				say "$indent$k, with contents:";
			}
			else {
				say "$indent$k, an empty directory";
			}
		}
		else {
			say "$indent$k";
		}
		
		dump_data_for_path( $data->{ $k }, "$indent  " );
	}
}
